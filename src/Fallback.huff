/// @title Fallback
/// @notice SPDX-License-Identifier: MIT
/// @author mektigboy

/////////////////
/// INTERFACE ///
/////////////////

#define function contributions(address) view returns (uint256)
#define function owner() view returns (address)
#define function contribute() payable returns ()
#define function getContribution() view returns (uint256)
#define function withdraw() nonpayable returns ()

///////////////
/// STORAGE ///
///////////////

#define constant OWNER_SLOT = FREE_STORAGE_POINTER()

/////////////////
/// MODIFIERS ///
/////////////////

/// @notice Modifier to check if the caller is the owner
/// @dev Reverts if the caller is not the owner
#define macro ONLY_OWNER() = takes (0) returns (0) {
    // Check if the caller is the owner
    caller                          // [msg.sender]
    [OWNER_SLOT]                    // [slot, msg.sender]
    sload                           // [owner, msg.sender]
    eq                              // [bool]
    authed                          // []
    jumpi

    // Revert if the caller is not the owner
    0x00                            // [0]
    0x00                            // [0, 0]
    revert

    // If the caller is the owner, continue
    authed:                    
}

//////////////
/// MACROS ///
//////////////

/// @notice Get the slot from a key
/// @param {key} [bytes32] - The key
/// @return {slot} [bytes32] - The slot
#define macro GET_SLOT_FROM_KEY(pointer) = takes(1) returns (1) {
    // Input stack: [key]
    // Load the data into memory and hash it, while preserving the memory location
    <pointer>                       // [<pointer>, key]
    mstore                          // []

    // Hash the data, generating a key
    0x20                            // [32]
    <pointer>                       // [<pointer>, 32]
    sha3                            // [slot]
}

//////////////////////
/// CONTRACT LOGIC ///
//////////////////////

/// @notice Get the contribution of a contributor
/// @param {contributor} [address] - The contributor
/// @return {contribution} [uint256] - The contribution
#define macro CONTRIBUTIONS() = takes (0) returns (0) {
    // Copy the contribution to memory
    0x04                            // [4]
    calldataload                    // [key]
    GET_SLOT_FROM_KEY(0x00)         // [slot]
    sload                           // [contribution]
    0x00                            // [0, contribution]
    mstore                          // []

    // Return the contribution
    0x20                            // [32]
    0x00                            // [0, 32]
    return
}

/// @notice Get the owner of the contract
/// @return {owner} [address] - The owner
#define macro OWNER() = takes (0) returns (0) {
    // Copy the owner to memory
    [OWNER_SLOT]                    // [slot]
    sload                           // [owner]
    0x00                            // [0, owner]       
    mstore                          // []

    // Return the owner
    0x20                            // [32]
    0x00                            // [0, 32]
    return
}

/// @notice Contribute to the contract
/// @dev Reverts if `msg.value` is less than 0.001 ETH
#define macro CONTRIBUTE() = takes (0) returns (0) {
    // Check if `msg.value` is less than 0.001 ETH
    callvalue                       // [msg.value]
    0x38d7ea4c68000                 // [100000000000000000, msg.value]
    gt                              // [bool]
    revert_call                     // []
    jumpi

    // Get the current contribution
    caller                          // [msg.sender]
    GET_SLOT_FROM_KEY(0x00)         // [slot]
    dup1                            // [slot, slot]
    sload                           // [contribution, slot
    callvalue                       // [msg.value, contribution, slot]
    add                             // [sum, slot]
    dup1                            // [sum, sum, slot]
    swap2                           // [slot, sum, sum]
    sstore                          // [sum]

    // Check if the contribution is greater than the owner's
    [OWNER_SLOT]                    // [slot, sum]
    sload                           // [owner, sum]
    GET_SLOT_FROM_KEY(0x00)         // [slot, sum]
    sload                           // [contribution, sum]
    lt                              // [bool]
    set_owner                   // []
    jumpi

    stop
    
    // Set the new owner
    set_owner:
        caller                      // [msg.sender] 
        [OWNER_SLOT]                // [slot, msg.sender]
        sstore                      // []

        stop

    // Revert if `msg.value` is less than 0.001 ETH
    revert_call:
        0x00                        // [0]
        0x00                        // [0, 0]
        revert
}

/// @notice Get the contribution of `msg.sender`
/// @return {contribution} [uint256] - The contribution
#define macro GET_CONTRIBUTION() = takes (0) returns (0) {
    caller                          // [msg.sender]
    GET_SLOT_FROM_KEY(0x00)         // [slot]
    sload                           // [contribution]
    0x00                            // [0, contribution]
    mstore                          // []

    // Return the contribution
    0x20                            // [32]
    0x00                            // [0, 32]
    return
}

/// @notice Withdraw the balance of the contract
/// @dev Reverts if the caller is not the owner
#define macro WITHDRAW() = takes (0) returns (0)  {
    ONLY_OWNER()

    0x00                            // [0]
    0x00                            // [0, 0]
    0x00                            // [0, 0, 0]
    0x00                            // [0, 0, 0, 0]
    address                         // [address, 0, 0, 0, 0]
    balance                         // [balance, 0, 0, 0, 0]
    [OWNER_SLOT]                    // [slot, balance, 0, 0, 0, 0]
    sload                           // [owner, balance, 0, 0, 0, 0]
    gas                             // [gas, owner, balance, 0, 0, 0, 0]
    call                            // [bool]      
    iszero                          // [bool]
    revert_call                     // []
    jumpi

    stop

    // Revert if the call failed
    revert_call:
        0x00                        // [0]
        0x00                        // [0, 0]
        revert                      
}

///////////////////
/// CONSTRUCTOR ///
///////////////////

/// @notice Constructor
/// @dev Sets `msg.sender` as the owner
#define macro CONSTRUCTOR() = takes (0) returns (0) {
    // Copy the owner to memory
    0x20                            // [32]
    0x20                            // [32, 32]
    codesize                        // [32, 32, 32]
    sub                             // [0, 32]
    0x00                            // [0, 0, 32]
    codecopy                        // []

    // Set the owner
    0x00                            // [0]
    mload                           // [owner]
    [OWNER_SLOT]                    // [slot, owner]
    sstore                          // []

    // Set the contribution of the owner to 1000 ETH
    0x3635c9adc5dea00000            // [1000000000000000000000]
    0x00                            // [0, 1000000000000000000000]
    mload                           // [owner, 1000000000000000000000]
    GET_SLOT_FROM_KEY(0x00)         // [slot, 1000000000000000000000]
    sstore                          // []
}

////////////////////////
/// MAIN ENTRY POINT ///
////////////////////////

/// @notice Main entry point
#define macro MAIN() = takes (0) returns (0) {
    // Identify which function is being called
    0x00                            // [0]
    calldataload                    // [sig]
    0xE0                            // [224, sig, 0]
    shr                             // [sel, 0]

    // Jump to the function depending on the selector
    dup1 __FUNC_SIG(contributions) eq contributionsJump jumpi
    dup1 __FUNC_SIG(owner) eq ownerJump jumpi
    dup1 __FUNC_SIG(contribute) eq contributeJump jumpi
    dup1 __FUNC_SIG(getContribution) eq getContributionJump jumpi
    dup1 __FUNC_SIG(withdraw) eq withdrawJump jumpi

    // Jump into the receive function if `msg.value` is greater than 0
    // and the contribution of `msg.sender` is greater than 0
    0x00                            // [0]
    callvalue                       // [msg.value, 0]
    gt                              // [bool]
    0x00                            // [0, bool]
    caller                          // [msg.sender, 0, bool]
    GET_SLOT_FROM_KEY(0x00)         // [slot, 0, bool]
    sload                           // [contribution, 0, bool]
    gt                              // [bool, bool]
    and                             // [bool]
    receive                         // []
    jumpi

    // Revert if the function is not found
    0x00                            // [0]
    0x00                            // [0, 0]
    revert       

    // Function jump destinations
    contributionsJump:
        CONTRIBUTIONS()

    ownerJump:
        OWNER()

    contributeJump:
        CONTRIBUTE()

    getContributionJump:
        GET_CONTRIBUTION()

    withdrawJump:
        WITHDRAW()

    receive:
        RECEIVE()
}

///////////////
/// RECEIVE ///
///////////////

/// @notice Receive function
#define macro RECEIVE() = {
    caller                      // [msg.sender]
    [OWNER_SLOT]                // [slot, msg.sender]
    sstore                      // []   
}
